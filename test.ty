import os (..)
import sh (sh)
import chalk (..)
import time

let start = time.utime()

let passed = 0
let failed = 0
let timedOut = 0

let g = pen(bold, green)
let r = pen(bold, red)
let m = pen(bold, brightMagenta)
let b = pen(bold, blue)

let testThreads = []

for test in listdir('tests') {
	testThreads.push(Thread((name) -> {
		let result = if not let $out = sh("./ty tests/{name}", timeoutMs: 2000) {
			timedOut += 1
			m('TIMEOUT')
		} else {
			match out.strip() {
				'PASS' => {
					passed += 1
					g('PASS')
				},

				_      => {
					failed += 1
					r('FAIL')
				}
			}
		}

		print("{b(name.comb(/\.ty/))} ... {result}")
	}, test))
}

testThreads.map(&join)

let elapsedSec = (time.utime() - start) / (1000.0 * 1000.0)

if passed > 0 {
	print("{g('Passed')}: {g(passed)}")
}

if failed > 0 {
	print("{r('Failed')}: {r(failed)}")
}

if timedOut > 0 {
	print("{m('Timed out')}: {m(timedOut)}")
}

print("Elapsed: {b(str(elapsedSec) + 's')}")

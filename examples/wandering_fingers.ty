import fs

let Ok(words) = fs::File.slurp('enable1.txt').map(.split(/\s+/));

function match?(big, small) {
     if (big.char(0) != small.char(0) || big.char(-1) != small.char(-1))
          return false;

     let i = 0;
     for c in small.chars() match big.search(c, i) {
          nil => { return false; },
          idx => { i = idx;      }
     }

     return true;
}

while let $word = read() {
     match words.filter(w -> match?(word, w)) {
          [] => {
               print("There are no words matching '{word}'");
          },
          ws => {
               print("Words matching '{word}':");
               for [i, w] in ws.enumerate!() {
                    print("\t{i + 1}. {w}");
               }
          }
     }
}
